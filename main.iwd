-- Collection of games made in I Wanna Die

--root
clear
set #bugHat, " "
set #bugBug, "🐞"
set #bugHand, " "
set #bugLegs, " "

goto "gameSelector"

@gameSelector -- Game Selector Marker
clear
print "Bug Wanna Die 🐞"
print " "
print "R - Roulette 🐜"
print "P - Rock Paper & Scissors 🐝"
print "H - Hanged 🦋"
print " "
print "D - Bug DressUp 🎀"
print "G - Bug gallery 🖼️"
print "C - Credits 🐌"
print " "
set #gameSelected, [input "[r, p, h, ...] >> "]
goif "r", [check [add #gameSelected, 0], "r"]
goif "r", [check [add #gameSelected, 0], "R"]
goif "p", [check [add #gameSelected, 0], "p"]
goif "p", [check [add #gameSelected, 0], "P"]
goif "h", [check [add #gameSelected, 0], "h"]
goif "h", [check [add #gameSelected, 0], "H"]
goif "i", [check [add #gameSelected, 0], "i"]
goif "i", [check [add #gameSelected, 0], "I"]
goif "c", [check [add #gameSelected, 0], "c"]
goif "c", [check [add #gameSelected, 0], "C"]
goif "d", [check [add #gameSelected, 0], "d"]
goif "d", [check [add #gameSelected, 0], "D"]
goif "g", [check [add #gameSelected, 0], "g"]
goif "g", [check [add #gameSelected, 0], "G"]
goif "...", [check [add #gameSelected, 0], "..."]

goto "bug"
die

-- Roulette Minigame ------------------------
@r
clear
print "Roulette game 🎲"

-- Roulette Variables
set #slots, 6
set #bullet, [floor [random 1, 6]]

print " "
input "Enter for start "
goto "r-shoot-u"
die

@r-shoot-u
clear
print "You turn 🐌"
print " "
input "Shooting... 🔫 "
print " "
goif "r-die-u", [check [add #slots, 0], [add #bullet, 0]]
input "Nothing happened 👼 "
set #slots, [floor [add #slots, -1]]
goto "r-shoot-bug"
die

@r-shoot-bug
clear
print "Bug turn 🐜"
print " "
input "Shooting... 🔫 "
print " "
goif "r-die-bug", [check [add #slots, 0], [add #bullet, 0]] 
input "Nothing happened 👼 "
set #slots, [floor [add #slots, -1]]
goto "r-shoot-u"
die

@r-die-u
input "You died 💀. Bug win "
goto "gameSelector"
die

@r-die-bug
input "Bug died 💀. You win "
goto "gameSelector"
die

-- RPS Game ---------------------------------
@p
clear
print "Rock Paper & Scissors 🏹"

-- Rps Variables
set #playerScore, [floor 0]
set #playerChoice, ""
set #bugScore, [floor 0]
set #bugChoice, ""
set #round, 1

print " "
input "Enter for start "
goto "p-game"
die

@p-game
clear
goif "p-u-win", [check [add #playerScore, 0], 3]
goif "p-bug-win", [check [add #bugScore, 0], 3]
print [text "Round ", [text [int [add #round, 0]], " 🦟"]]
print [text [text [add #playerScore, 0], " : "], [add #bugScore, 0]]
print " "
set #playerChoice, [input "[r, p, s] >> "]
goif "p-bug", [not [or [check [add #playerChoice, 0], "r"], [or [check [add #playerChoice, 0], "s"], [check [add #playerChoice, 0], "p"]]]]

set #bugChoice, [floor [random 1, 3]]

doif [check [add #bugChoice, 0], 1]
set #bugChoice, "r"
doif [check [add #bugChoice, 0], 2]
set #bugChoice, "p"
doif [check [add #bugChoice, 0], 3]
set #bugChoice, "s"

goto "p-results"
die

@p-results
clear
print [text "🐌: ", [text [add #playerChoice, 0], [text " - ", [text [add #bugChoice, 0], " :🐝 "]]]]
print " "
set #round, [add #round, 1]
goif "p-tie", [check [add #bugChoice, 0], [add #playerChoice, 0]]
goif "p-u-score", [and [check [add #playerChoice, 0], "r"], [check [add #bugChoice, 0], "s"]]
goif "p-u-score", [and [check [add #playerChoice, 0], "s"], [check [add #bugChoice, 0], "p"]]
goif "p-u-score", [and [check [add #playerChoice, 0], "p"], [check [add #bugChoice, 0], "r"]]
goto "p-bug-score"
die

@p-tie
print " "
input "Tie 😐 " 
goto "p-game"
die

@p-u-score
print " "
input "U wins the round 🐌! "
set #playerScore, [floor [add #playerScore, 1]]
goto "p-game"
die

@p-bug-score
input "Bug wins the round 🐝! "
set #bugScore, [floor [add #bugScore, 1]]
goto "p-game"
die

@p-u-win
input "U wins the game 🐌! "
goto "gameSelector"
die

@p-bug-win
input "Bug wins the game 🐝! "
goto "gameSelector"
die

@p-bug
clear
input "Wrong selection 😐 "
goto "p-game"
die

-- HANGED -----------------------------------
@h
clear
print "Hanged 🧵"

-- Hanged Variables
set #word, "BUG"
set #letter1, "_"
set #letter2, "_"
set #letter3, "_"
set #losses, 0
set #head, " "
set #body, " "
set #legs, " "

print " "
input "Enter for start "
goto "h-game"
die

@h-game
clear
print [text [text [add #letter1, 0], [add #letter2, 0]], [add #letter3, 0]]
print "_"
print " \"
print [text " ", [add #head, 0]]
print [text " ", [add #body, 0]]
print [text " ", [add #legs, 0]]
print "  "
goif "h-dead", [check [add #losses, 0], 5]
goif "h-win", [and [and [check [add #letter1, 0], "B"], [check [add #letter2, 0], "U"]], [check [add #letter3, 0], "G"]]
set #playerLetter, [input "Letter: "]
goto "h-checker"
die

@h-checker
goif "h-find-b", [check [add #playerLetter, 0], "b"]
goif "h-find-b", [check [add #playerLetter, 0], "B"]
goif "h-find-u", [check [add #playerLetter, 0], "u"]
goif "h-find-u", [check [add #playerLetter, 0], "U"]
goif "h-find-g", [check [add #playerLetter, 0], "g"]
goif "h-find-g", [check [add #playerLetter, 0], "G"]
goto "h-no-find"
die

@h-find-b
clear
goif "h-no-find", [check [add #letter1, 0], "B"]
input "You find the letter b! "
set #letter1, "B"
goto "h-game"
die

@h-find-u
clear
goif "h-no-find", [check [add #letter2, 0], "U"]
input "You find the letter u! "
set #letter2, "U"
goto "h-game"
die

@h-find-g
clear
goif "h-no-find", [check [add #letter3, 0], "G"]
input "You find the letter g! "
set #letter3, "G"
goto "h-game"
die

@h-no-find
clear
input "Incorrect or repeat letter 🦋 "
set #losses, [add #losses, 1]
doif [check [add #losses, 0], 1]
set #head, "😊"
doif [check [add #losses, 0], 2]
set #body, " |"
doif [check [add #losses, 0], 3]
set #legs, "/"
doif [check [add #losses, 0], 4]
set #legs, "/ \"
doif [check [add #losses, 0], 5]
set #head, "💀"
goto "h-game"
die

@h-dead
input "HANGED! 🦋 "
goto "gameSelector"
die

@h-win
input "You win!!! The word is BUG 🐞 "
goto "gameSelector"
die

-- Bug Dressup ------------------------------
@d
clear
print "Bug Dress Up 🎀"
print " "
print "[you can put any char or emoji for dress your bug]"
set #dSetter, 0
print " "
input "Enter for start "
goto "d-design"
die

@d-design
clear
print "Design Bug 👚"
print " "
print [add #bugHat, 0]
print [text [add #bugBug, 0], [add #bugHand, 0]]
print [add #bugLegs, 0]
print " "
goif "d-hat", [check [add #dSetter, 0], 0]
goif "d-bug", [check [add #dSetter, 0], 1]
goif "d-hand", [check [add #dSetter, 0], 2]
goif "d-legs", [check [add #dSetter, 0], 3]
input "You bug is pretty cute! You finished (try put 'i' in main menu) "
goto "gameSelector"
die

@d-hat
set #bugHat, [input "[🎩, 👒] >> "]
set #dSetter, [add #dSetter, 1]
goto "d-design"
die

@d-bug
set #bugBug, [input "[🐞, 🐌, 🦂, 🐜] >> "]
set #dSetter, [add #dSetter, 1]
goto "d-design"
die

@d-hand
set #bugHand, [input "[🔫, 🌼, 🥊] >> "]
set #dSetter, [add #dSetter, 1]
goto "d-design"
die

@d-legs
set #bugLegs, [input "[👟, 🛒, 🚲] >> "]
set #dSetter, [add #dSetter, 1]
goto "d-design"
die

-- Bug Gallery ------------------------------
@g 
clear
set #galleryCount, 0
print "Bug gallery 🖼️"
print " "
print "A gallery of bugs made with the bug designer, If you want your bug to be here, send a photo of it on twitter with #BugWannaDie"
print " "
input "Enter for view the gallery "
goto "g-set"
die

@g-set
goif "g-lajbel", [check [add #galleryCount, 0], 0]
goif "g-antikore", [check [add #galleryCount, 0], 1]
goif "g-kiveygaming", [check [add #galleryCount, 0], 2]
set #galleryCount, 0
goto "g-set"
die

@g-show
clear
print [add #gTitle, 0]
print "============================"
print "|                          |"
print "|                          |"
print [text "|           ", [text [add #gBugHat, 0], "             |"]]
print [text [text "|           ", [add #gBugBug, 0]], [text [add #gBugHand, 0], "           |"]]
print [text "|           ", [text [add #gBugLegs, 0], "             |"]]
print "|                          |"
print "|                          |"
print "============================"
print [text "by: ", [add #gAuthor, 0]]
print " "
set #gSelection, [input "Enter for continue or type exit for go to main menu >> "]
doif [or [check [add #gSelection, 0], "exit"], [check [add #gSelection, 0], "EXIT"]]
goto "gameSelector"
set #galleryCount, [add #galleryCount, 1]
goto "g-set"
die

@g-lajbel
set #gBugHat, "🎩"
set #gBugBug, "🐞"
set #gBugHand, "🍷"
set #gBugLegs, "🌏" 
set #gTitle, "mr bug"
set #gAuthor, "LaJBel"
goto "g-show"
die

@g-antikore
set #gBugHat, "🤠"
set #gBugBug, "👕"
set #gBugHand, "🔫"
set #gBugLegs, "🥾" 
set #gTitle, "Cowboy"
set #gAuthor, "Antikore"
goto "g-show"
die

@g-kiveygaming
set #gBugHat, "  "
set #gBugBug, "🦜"
set #gBugHand, "  "
set #gBugLegs, "  " 
set #gTitle, "Parrot"
set #gAuthor, "KiveyGaming"
goto "g-show"
die

-- Credits ----------------------------------
@c
clear
print "BUG WANNA DIE 🐞"
print "for the #GameOff2021 😺"
print " "
print "Credits"
print "🐌 LaJBel - Code and Minigames"
print "🦂 Antikore - IWANNADIE creator and some minigames"
print " "
print "Itch: https://lajbel.itch.io/bug-wanna-die"
print "GitHub: https://github.com/lajbel/bug-wanna-die"
print " "
print "This game have 444 lines"
print " "
input "Enter for back to main menu"
goto "gameSelector"
die

-- For stupids ------------------------------
@...
clear
print "You are stupid? 🐞"
input "THE DAMN 3 POINTS MEANS YOU MUST ENTER THE LETTER OF THE GAME YOU WANT TO ENTER "
goto "gameSelector"
die

-- BUG WANANA DIE ---------------------------
@i
clear
print " ________________________________________ " 
print "/ I WANNA DIEEEEEEEEEEEEEEEEEEEEEEEEEEEE \"
print " ---------------------------------------- "
print "          \                               "
print "           \                              "
print [text "            ", [text [add #bugHat, 0], "                             "]]
print [text [text "            ", [add #bugBug, 0]], [text [add #bugHand, 0], "                             "]]
input [text "            ", [text [add #bugLegs, 0], "                             "]]

goto "gameSelector"
die

-- Bug marker -------------------------------
@bug
clear
input "Selected game no exits 🐛 "
goto "gameSelector"
die